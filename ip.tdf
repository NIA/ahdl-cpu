SUBDESIGN IP
(
  AddrIn[7..0],  % adress for reseting %
  ModeInc,        % increment (1) or reset (0) %
  ENA,            % enable input %
  CLK             % clock %
    : INPUT;

  AddrOut[7..0],  % address of current instruction %
  Overflow        % if reached address #FF and incremented %
    : OUTPUT;
)

VARIABLE
  trig[7..0]   : DFFE;  
  inced[7..0]  : NODE; % Incremented data %
  crr[8..1]    : NODE; % Carry %

BEGIN
  trig[].clk = CLK;
  trig[].ena = ENA;

  crr[1] = trig[0].q; % carry from first if it's 1+1 %
  crr[8..2] = trig[7..1] & crr[7..1];
  
  inced[0] = trig[0].q $ VCC;
  inced[7..1] = trig[7..1] $ crr[7..1];

  trig[].d =  ModeInc & inced[]
           # !ModeInc & AddrIn[];

  Overflow = ENA & ModeInc & crr[8];
  AddrOut[] = trig[].q;
END;
